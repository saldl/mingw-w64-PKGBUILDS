pkgname=mingw-w64-curl
pkgver=7.45.0
pkgrel=1
pkgdesc="An URL retrival utility and library (mingw-w64)"
arch=('any')
url="http://curl.haxx.se"
license=("MIT")
depends=('mingw-w64-crt'
         'mingw-w64-openssl'
         'mingw-w64-nghttp2'
         'mingw-w64-zlib')
makedepends=('mingw-w64-configure')
options=('staticlibs' '!strip' '!buildflags')
source=("$url/download/curl-$pkgver.tar.bz2")
md5sums=('62c1a352b28558f25ba6209214beadc8')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd "${srcdir}"/${pkgname#mingw-w64-}-${pkgver}
  for _arch in ${_architectures}; do
    configure_args="--with-ssl --enable-ipv6 --with-nghttp2 --without-ca-bundle --without-random"
    mkdir -p build-${_arch}-static && pushd build-${_arch}-static
    CPPFLAGS='-DNGHTTP2_STATICLIB' ${_arch}-configure $configure_args \
      --enable-static --disable-shared
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname#mingw-w64-}-${pkgver}/build-${_arch}-static"
    make DESTDIR="${pkgdir}" install
    rm "$pkgdir"/usr/${_arch}/bin/*.exe
    rm -r "${pkgdir}/usr/${_arch}/share"
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
  done
}
