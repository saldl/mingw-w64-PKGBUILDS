pkgname=mingw-w64-libevent-saldl
pkgver=2.1.5.beta.137.g1acffc0
pkgrel=1
pkgdesc="An event notification library (mingw-w64) / saldl repo"
arch=(any)
url="https://github.com/saldl/libevent"
license=("BSD")
makedepends=(mingw-w64-gcc mingw-w64-pkg-config)
depends=(mingw-w64-crt mingw-w64-openssl)
options=(!libtool !strip !buildflags)
source=(${pkgname}::git://github.com/saldl/libevent.git)
md5sums=('SKIP')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

pkgver() {
  cd "${srcdir}/${pkgname}"
  ver="$(expr "`git describe --dirty --long`" : 'release-\(.*\)')"
  echo "${ver//-/.}"

}

prepare() {
  cd "${srcdir}/${pkgname}"
  ./autogen.sh
}

build() {
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p "${srcdir}/${pkgname}-build-${_arch}"
    cd "${srcdir}/${pkgname}-build-${_arch}"
    ${srcdir}/${pkgname}/configure \
      --disable-shared  --disable-openssl --disable-samples \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch}
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' -o -name '*.def' -o -name '*.exp' -o -name '*.py' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
  done
}
